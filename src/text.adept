
import cstdio

func getTextIndex(text String, position Position) <usize> Optional {
    line usize = 0
    character usize = 0

    for i usize = 0; i < text.length; i++ {
        if line == position.line and character == position.character {
            return some(i)
        }

        if text[i] == '\n'ub {
            line++
            character = 0
        } else {
            character++
        }
    }

    return none()
}

func getTextPositionInContent(content String, index usize) Position {
    line, character usize = 0

    for count usize = 0; count < index; count++ {
        if content[count] == '\n'ub {
            line++
            character = 0
        } else {
            character++
        }
    }

    return Position(line, character)
}

func getTextPositionInFile(filename String, index usize) <Position> Optional {
    filename_cstr *ubyte = filename.cstr()
    defer delete filename_cstr

    line, character usize = 0

    f *FILE = fopen(filename_cstr, 'r')

    if f == null {
        return none()
    }
    defer fclose(f)

    buf 1 ubyte

    for count usize = 0; count < index; count++ {
        res int = fread(&buf, sizeof ubyte, 1, f)

        if res == 0 {
            return none()
        }

        if res < 0 {
            return none()
        }

        if buf[0] == '\n'ub {
            line++
            character = 0
        } else {
            character++
        }
    }

    return some(Position(line, character))
}

