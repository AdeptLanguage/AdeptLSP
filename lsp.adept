
import JSON
import "headers.adept"

struct Message (method String, id JSON) {
    func toString String {
        return sprintf("method=%S, id=%S", this.method, this.getIDString())
    }

    func getIDString String {
        number <double> Optional = this.id.number()

        if number.has {
            return toString(number.get() as long)
        }

        return toString(this.id)
    }
}

func lsp\readMessage() *Message {
    message *Message = new Message

    json JSON = readJSON()

    log("[received] %S\n", toString(json))

    message.id = json.field("id").clone()
    message.method = json.field("method").string().orElse("").toOwned()

    return message
}

func readJSON() JSON {
    headers Headers = readHeaders()

    capacity usize = headers.content_length + 1
    buffer *ubyte = new ubyte * capacity

    num_read usize = fread(buffer, 1, headers.content_length, stdin)

    if num_read != headers.content_length {
        fprintf(stderr, "Error: num_read != headers.content_length\n")
        abort()
    }

    json_text String = String(buffer, num_read, capacity, ::GIVEN)
    log("[read] %S\n", json_text)
    return JSONFromString(json_text)
}

func lsp\writeMessage(json JSON) {
    content String = json.serialize()
    log("[sending] %S\n", content)

    printf("Content-Length: %d\r\n\r\n%S", content.length, content)
    fflush(stdout)
}
